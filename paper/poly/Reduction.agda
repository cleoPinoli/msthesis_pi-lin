open import Data.Fin using (Fin; suc)
open import Data.Nat using (suc)
open import Data.Bool using (Bool; true; false; if_then_else_)
open import Data.Product using (_Ã—_; _,_; âˆƒ; Î£; Î£-syntax; âˆƒ-syntax)
open import Relation.Binary.PropositionalEquality using (_â‰¡_; refl; sym; cong; congâ‚‚)
open import Data.List.Base using (List; []; _âˆ·_; [_]; _++_)
open import Data.List.Properties using (++-assoc)

open import Type
open import Context
open import Permutations
open import Process
open import Congruence

weakening : âˆ€{n} {Î“ Î“â‚ Î“â‚‚ : Context n} (un : Un Î“â‚) â†’ Î“ â‰ƒ Î“â‚ + Î“â‚‚ â†’ Process Î“â‚‚ â†’ Process Î“
weakening un p P = #process (+++# p) (aux un P)
  where
    aux : âˆ€{Î“â‚ Î“â‚‚} (un : Un Î“â‚) â†’ Process Î“â‚‚ â†’ Process (Î“â‚ ++ Î“â‚‚)
    aux un-[] P = P
    aux (un-âˆ· un) P = weaken (split-l +-unit-l) (aux un P)

contraction : âˆ€{n} {Î“ Î“â‚ Î“â‚‚ : Context n} (un : Un Î“â‚) â†’ Î“ â‰ƒ Î“â‚ + Î“â‚‚ â†’ Process (Î“â‚ ++ Î“) â†’ Process Î“
contraction un p P = #process (+++# p) (aux un (#process (#left (#sym (+++# p))) P))
  where
    aux : âˆ€{Î“â‚ Î“â‚‚} â†’ Un Î“â‚ â†’ Process (Î“â‚ ++ Î“â‚ ++ Î“â‚‚) â†’ Process (Î“â‚ ++ Î“â‚‚)
    aux un-[] P = P
    aux {Â¿ A âˆ· Î“â‚} {Î“â‚‚} (un-âˆ· un) P with contract (split-l +-unit-l) (#process (#shift {_} {Â¿ A} {Â¿ A âˆ· Î“â‚} {Î“â‚ ++ Î“â‚‚}) P)
    ... | Pâ‚ rewrite sym (++-assoc (Â¿ A âˆ· Î“â‚) Î“â‚ Î“â‚‚) with #process (#sym (#shift {_} {Â¿ A} {Î“â‚ ++ Î“â‚})) Pâ‚
    ... | Pâ‚‚ rewrite ++-assoc Î“â‚ Î“â‚ (Â¿ A âˆ· Î“â‚‚) with aux un Pâ‚‚
    ... | Pâ‚ƒ = #process #shift Pâ‚ƒ

dual-subst : âˆ€{m n} {A A' : Type m} {B : Type n} {Ïƒ : Fin m -> Type n} -> Dual A A' -> Subst Ïƒ A B -> âˆƒ[ B' ] Dual B B' Ã— Subst Ïƒ A' B'
dual-subst d-ğŸ˜-âŠ¤ s-ğŸ˜ = âŠ¤ , d-ğŸ˜-âŠ¤ , s-âŠ¤
dual-subst d-ğŸ™-âŠ¥ s-ğŸ™ = âŠ¥ , d-ğŸ™-âŠ¥ , s-âŠ¥
dual-subst d-âŠ¥-ğŸ™ s-âŠ¥ = ğŸ™ , d-âŠ¥-ğŸ™ , s-ğŸ™
dual-subst d-âŠ¤-ğŸ˜ s-âŠ¤ = ğŸ˜ , d-âŠ¤-ğŸ˜ , s-ğŸ˜
dual-subst (d-!-? d) (s-! s) with dual-subst d s
... | B' , d' , s' = Â¿ B' , d-!-? d' , s-? s'
dual-subst (d-?-! d) (s-? s) with dual-subst d s
... | B' , d' , s' = Â¡ B' , d-?-! d' , s-! s'
dual-subst (d-&-âŠ• d e) (s-& s t) with dual-subst d s | dual-subst e t
... | B' , d' , s' | C' , e' , t' = (B' âŠ• C') , d-&-âŠ• d' e' , s-âŠ• s' t'
dual-subst (d-âŠ•-& d e) (s-âŠ• s t) with dual-subst d s | dual-subst e t
... | B' , d' , s' | C' , e' , t' = (B' & C') , d-âŠ•-& d' e' , s-& s' t'
dual-subst (d-âŠ—-â…‹ d e) (s-âŠ— s t) with dual-subst d s | dual-subst e t
... | B' , d' , s' | C' , e' , t' = (B' â…‹ C') , d-âŠ—-â…‹ d' e' , s-â…‹ s' t'
dual-subst (d-â…‹-âŠ— d e) (s-â…‹ s t) with dual-subst d s | dual-subst e t
... | B' , d' , s' | C' , e' , t' = (B' âŠ— C') , d-â…‹-âŠ— d' e' , s-âŠ— s' t'
dual-subst (d-âˆƒ-âˆ€ d) (s-âˆƒ s) with dual-subst d s
... | B' , d' , s' = $âˆ€ B' , d-âˆƒ-âˆ€ d' , s-âˆ€ s'
dual-subst (d-âˆ€-âˆƒ d) (s-âˆ€ s) with dual-subst d s
... | B' , d' , s' = $âˆƒ B' , d-âˆ€-âˆƒ d' , s-âˆƒ s'
dual-subst d-v-o (s-r (s-var d)) = _ , d , s-r (s-ort d)
dual-subst d-o-v (s-r (s-ort d)) = _ , dual-symm d , s-r (s-var d)

data _â†_ {n} {Î“ : Context n} : Process Î“ â†’ Process Î“ â†’ Set where
  r-link      : âˆ€{Î” A B} {P : Process (B âˆ· Î”)}
                (d : Dual A B) (e : Dual A B) (p : Î“ â‰ƒ B , Î”) â†’
                cut d p (link e (split-l (split-r split-e))) P â† #process (#cons p) P
  r-close     : âˆ€{P : Process Î“} (pâ‚€ : Î“ â‰ƒ [] + Î“) (qâ‚€ : Î“ â‰ƒ [] + Î“) â†’
                cut d-ğŸ™-âŠ¥ pâ‚€ close (wait (split-l qâ‚€) P) â† P
  r-select-l  : âˆ€{Î“â‚ Î“â‚‚ A Aâ€² B Bâ€²}
                {P : Process (A âˆ· Î“â‚)} {Q : Process (Aâ€² âˆ· Î“â‚‚)} {R : Process (Bâ€² âˆ· Î“â‚‚)}
                (d : Dual A Aâ€²) (e : Dual B Bâ€²)
                (p : Î“ â‰ƒ Î“â‚ + Î“â‚‚) (pâ‚€ : Î“â‚ â‰ƒ [] + Î“â‚) (qâ‚€ : Î“â‚‚ â‰ƒ [] + Î“â‚‚) â†’
                cut (d-âŠ•-& d e) p
                    (select true (split-l pâ‚€) P)
                    (case (split-l qâ‚€) Q R) â† cut d p P Q
  r-select-r  :
    âˆ€{Î“â‚ Î“â‚‚ A Aâ€² B Bâ€²}
    {P : Process (B âˆ· Î“â‚)} {Q : Process (Aâ€² âˆ· Î“â‚‚)} {R : Process (Bâ€² âˆ· Î“â‚‚)}
    (d : Dual A Aâ€²) (e : Dual B Bâ€²) (p : Î“ â‰ƒ Î“â‚ + Î“â‚‚) (pâ‚€ : Î“â‚ â‰ƒ [] + Î“â‚) (qâ‚€ : Î“â‚‚ â‰ƒ [] + Î“â‚‚) â†’
    cut (d-âŠ•-& d e) p
        (select false (split-l pâ‚€) P)
        (case (split-l qâ‚€) Q R) â† cut e p P R
  r-fork      :
    âˆ€{Î“â‚ Î“â‚‚ Î“â‚ƒ Î” A B Aâ€² Bâ€²}
    {P : Process (A âˆ· Î“â‚)} {Q : Process (B âˆ· Î“â‚‚)} {R : Process (Bâ€² âˆ· Aâ€² âˆ· Î“â‚ƒ)}
    (d : Dual A Aâ€²) (e : Dual B Bâ€²) (p : Î“ â‰ƒ Î” + Î“â‚ƒ) (pâ‚€ : Î“â‚ƒ â‰ƒ [] + Î“â‚ƒ)
    (q : Î” â‰ƒ Î“â‚ + Î“â‚‚) (qâ‚€ : Î” â‰ƒ [] + Î”) â†’
    let _ , pâ€² , qâ€² = +-assoc-l p q in
    cut (d-âŠ—-â…‹ d e) p
        (fork (split-l qâ‚€) q P Q)
        (join (split-l pâ‚€) R) â† cut d qâ€² P (cut e (split-r pâ€²) Q R)
  r-client    :
    âˆ€{Î“â‚ Î“â‚‚ A Aâ€²}
    {P : Process (A âˆ· Î“â‚)} {Q : Process (Aâ€² âˆ· Î“â‚‚)} (d : Dual A Aâ€²)
    (p : Î“ â‰ƒ Î“â‚ + Î“â‚‚) (pâ‚€ : Î“â‚ â‰ƒ [] + Î“â‚) (qâ‚€ : Î“â‚‚ â‰ƒ [] + Î“â‚‚) (un : Un Î“â‚) â†’
    cut (d-!-? d) p
      (server (split-l pâ‚€) un P)
      (client (split-l qâ‚€) Q) â† cut d p P Q
  r-weaken    :
    âˆ€{Î“â‚ Î“â‚‚ A Aâ€²}
    {P : Process (A âˆ· Î“â‚)} {Q : Process Î“â‚‚}
    (d : Dual A Aâ€²) (p : Î“ â‰ƒ Î“â‚ + Î“â‚‚) (pâ‚€ : Î“â‚ â‰ƒ [] + Î“â‚) (qâ‚€ : Î“â‚‚ â‰ƒ [] + Î“â‚‚) (un : Un Î“â‚) â†’
    cut (d-!-? d) p
        (server (split-l pâ‚€) un P)
        (weaken (split-l qâ‚€) Q) â† weakening un p Q
  r-contract  :
    âˆ€{Î“â‚ Î“â‚‚ A Aâ€²}
    {P : Process (A âˆ· Î“â‚)} {Q : Process (Â¿ Aâ€² âˆ· Â¿ Aâ€² âˆ· Î“â‚‚)}
    (d : Dual A Aâ€²) (p : Î“ â‰ƒ Î“â‚ + Î“â‚‚) (pâ‚€ : Î“â‚ â‰ƒ [] + Î“â‚) (qâ‚€ : Î“â‚‚ â‰ƒ [] + Î“â‚‚) (un : Un Î“â‚) â†’
    cut (d-!-? d) p
      (server (split-l pâ‚€) un P)
      (contract (split-l qâ‚€) Q) â†
      contraction un p
        (cut (d-!-? d) ++â‰ƒ+
             (server (split-l pâ‚€) un P)
             (cut (d-!-? d) (split-r p) (server (split-l pâ‚€) un P) Q))
  r-poly :
    âˆ€{A A' : Type (suc n)} {B C : Type n} {Î“â‚ Î“â‚‚ : Context n} (Ïƒ : Subst (make-subst C) A B)
    {P : Process (B âˆ· Î“â‚)} {F : {X Y : Type n} -> Subst (make-subst X) A' Y -> Process (Y âˆ· Î“â‚‚)}
    (d : Dual A A') (p : Î“ â‰ƒ Î“â‚ + Î“â‚‚) (pâ‚€ : Î“â‚ â‰ƒ [] + Î“â‚) (qâ‚€ : Î“â‚‚ â‰ƒ [] + Î“â‚‚) ->
    let B' , d' , Ïƒ' = dual-subst d Ïƒ in
    cut (d-âˆƒ-âˆ€ d) p (ex (split-l pâ‚€) Ïƒ P) (all (split-l qâ‚€) F) â†
    cut d' p P (F Ïƒ')
  r-cut       : âˆ€{Î“â‚ Î“â‚‚ A B} {P Q : Process (A âˆ· Î“â‚)} {R : Process (B âˆ· Î“â‚‚)}
                (d : Dual A B) (q : Î“ â‰ƒ Î“â‚ + Î“â‚‚) â†’ P â† Q â†’
                cut d q P R â† cut d q Q R
  r-cong      : âˆ€{P R Q : Process Î“} â†’ P âŠ’ R â†’ R â† Q â†’ P â† Q
