\section{Agda Formalization}
\label{sec:formalization}

\begin{code}[hide]
open import Data.Bool using (Bool; true; false; if_then_else_)
open import Data.Product using (_×_; _,_; ∃; ∃-syntax)
open import Relation.Binary.PropositionalEquality using (_≡_; refl; cong; cong₂)
open import Data.List.Base using (List; []; _∷_; [_]; _++_)
\end{code}

\subsection{Type Representation}
\label{sec:type-agda}

The representation of types is standard, with one constructor for each of the
forms described in \Cref{sec:calculus}.

\begin{code}
data Type : Set where
  𝟘 𝟙 ⊥ ⊤          : Type
  ¡ ¿              : Type → Type
  _&_ _⊕_ _⊗_ _⅋_  : Type → Type → Type
\end{code}

The notion of duality is formalized as a \emph{relation} \AgdaDatatype{Dual}
such that $\AgdaDatatype{Dual}~A~B$ holds if and only if $A = \dual{B}$.

\begin{code}
data Dual : Type → Type → Set where
  d-𝟘-⊤  : Dual 𝟘 ⊤
  d-⊤-𝟘  : Dual ⊤ 𝟘
  d-𝟙-⊥  : Dual 𝟙 ⊥
  d-⊥-𝟙  : Dual ⊥ 𝟙
  d-!-?  : ∀{A B} → Dual A B → Dual (¡ A) (¿ B)
  d-?-!  : ∀{A B} → Dual A B → Dual (¿ A) (¡ B)
  d-&-⊕  : ∀{A B A′ B′} → Dual A A′ → Dual B B′ → Dual (A & B) (A′ ⊕ B′)
  d-⊕-&  : ∀{A B A′ B′} → Dual A A′ → Dual B B′ → Dual (A ⊕ B) (A′ & B′)
  d-⊗-⅋  : ∀{A B A′ B′} → Dual A A′ → Dual B B′ → Dual (A ⊗ B) (A′ ⅋ B′)
  d-⅋-⊗  : ∀{A B A′ B′} → Dual A A′ → Dual B B′ → Dual (A ⅋ B) (A′ ⊗ B′)
\end{code}

It is straightforward to prove that duality is a symmetric relation and that it
behaves as an involution. From this we prove that it acts as the function
$\dual\cdot$ defined in \Cref{sec:calculus}.

\begin{code}
dual-symm   : ∀{A B} → Dual A B → Dual B A
dual-inv    : ∀{A B C} → Dual A B → Dual B C → A ≡ C
dual-fun-r  : ∀{A B C} → Dual A B → Dual A C → B ≡ C
dual-fun-l  : ∀{A B C} → Dual B A → Dual C A → B ≡ C
\end{code}
\begin{code}[hide]
dual-symm d-𝟘-⊤ = d-⊤-𝟘
dual-symm d-⊤-𝟘 = d-𝟘-⊤
dual-symm d-𝟙-⊥ = d-⊥-𝟙
dual-symm d-⊥-𝟙 = d-𝟙-⊥
dual-symm (d-!-? p) = d-?-! (dual-symm p)
dual-symm (d-?-! p) = d-!-? (dual-symm p)
dual-symm (d-&-⊕ p q) = d-⊕-& (dual-symm p) (dual-symm q)
dual-symm (d-⊕-& p q) = d-&-⊕ (dual-symm p) (dual-symm q)
dual-symm (d-⊗-⅋ p q) = d-⅋-⊗ (dual-symm p) (dual-symm q)
dual-symm (d-⅋-⊗ p q) = d-⊗-⅋ (dual-symm p) (dual-symm q)
\end{code}

\begin{code}[hide]
dual-inv d-𝟘-⊤ d-⊤-𝟘 = refl
dual-inv d-⊤-𝟘 d-𝟘-⊤ = refl
dual-inv d-𝟙-⊥ d-⊥-𝟙 = refl
dual-inv d-⊥-𝟙 d-𝟙-⊥ = refl
dual-inv (d-!-? p) (d-?-! q) = cong ¡ (dual-inv p q)
dual-inv (d-?-! p) (d-!-? q) = cong ¿ (dual-inv p q)
dual-inv (d-&-⊕ p q) (d-⊕-& r s) = cong₂ _&_ (dual-inv p r) (dual-inv q s)
dual-inv (d-⊕-& p q) (d-&-⊕ r s) = cong₂ _⊕_ (dual-inv p r) (dual-inv q s)
dual-inv (d-⊗-⅋ p q) (d-⅋-⊗ r s) = cong₂ _⊗_ (dual-inv p r) (dual-inv q s)
dual-inv (d-⅋-⊗ p q) (d-⊗-⅋ r s) = cong₂ _⅋_ (dual-inv p r) (dual-inv q s)

dual-fun-r d e = dual-inv (dual-symm d) e

dual-fun-l d e = dual-inv d (dual-symm e)
\end{code}

\subsection{Context Representation}
\label{sec:context-agda}

We are going to adopt a nameless representation of channels whereby a channel is
identified by its position in a typing context. This representation is akin to
using De Bruijn indices, except that the index, instead of being represented
explicitly by a natural number, is computable from the \emph{proof} that the
type of the channel belong to the typing context.
%
Typing contexts are represented using \emph{lists} of types, where the
(polymorphic) \AgdaDatatype{List} type is defined in Agda′s standard library.

\begin{code}
Context : Set
Context = List Type
\end{code}

The most important operation concerning typing contexts is \emph{splitting}. The
splitting of $\ContextC$ into $\ContextD$ and $\ContextE$, which is denoted by
$\Splitting\ContextC\ContextD\ContextE$, represents the fact that $\ContextC$
contains all the types contained in $\ContextD$ and $\ContextE$, preserving both
their overall \emph{multiplicity} and also their relative \emph{order} within
$\ContextD$ and $\ContextE$. A \emph{proof} of
$\Splitting\ContextC\ContextD\ContextE$ shows how the types in $\ContextC$ are
distributed in $\ContextD$ and $\ContextE$ from left to right.

\begin{code}[hide]
infix 4 _≃_+_ _≃_,_
\end{code}
\begin{code}
data _≃_+_ : Context → Context → Context → Set where
  split-e  : [] ≃ [] + []
  split-l  : ∀{A Γ Δ Θ} → Γ ≃ Δ + Θ → A ∷ Γ ≃ A ∷ Δ + Θ
  split-r  : ∀{A Γ Δ Θ} → Γ ≃ Δ + Θ → A ∷ Γ ≃ Δ + A ∷ Θ
\end{code}

As an example, below is the splitting of the context $[A, B, C, D]$ into $[A,
D]$ and $[B, C]$. Note how the sequence of \AgdaInductiveConstructor{split-l}
and \AgdaInductiveConstructor{split-r} applications determines where each of the
types in $[A, B, C, D]$ ends up in $[A, D]$ and $[B, C]$.

\begin{code}[hide]
module _ where
  postulate A B C D : Type
\end{code}
\begin{code}
  splitting-example : (A ∷ B ∷ C ∷ D ∷ []) ≃ (A ∷ D ∷ []) + (B ∷ C ∷ [])
  splitting-example = split-l (split-r (split-r (split-l split-e)))
\end{code}

It is often the case that the context $\ContextD$ in a splitting
$\Splitting\ContextC\ContextD\ContextE$ is a singleton list $[A]$. For this
particular case, we introduce a dedicated notation that allows us to represent
this case in a more compact and readable way, as
$\SimpleSplitting\ContextC{A}\ContextE$.

\begin{code}
_≃_,_ : Context → Type → Context → Set
Γ ≃ A , Δ = Γ ≃ [ A ] + Δ
\end{code}

Context splitting enjoys a number of expected properties. In particular, it is
easy to see that splitting is commutative and that the empty context is both a
left and right unit of splitting.

\begin{code}
+-comm    : ∀{Γ Δ Θ} → Γ ≃ Δ + Θ → Γ ≃ Θ + Δ
+-unit-l  : ∀{Γ} → Γ ≃ [] + Γ
+-unit-r  : ∀{Γ} → Γ ≃ Γ + []
\end{code}
\begin{code}[hide]
+-comm split-e = split-e
+-comm (split-l p) = split-r (+-comm p)
+-comm (split-r p) = split-l (+-comm p)

+-unit-l {[]} = split-e
+-unit-l {_ ∷ _} = split-r +-unit-l

+-unit-r = +-comm +-unit-l
\end{code}

Context splitting is also associative in a sense that is made precise below. If
we write $\ContextD + \ContextE$ for some $\ContextC$ such that $Γ ≃ Δ + Θ$, then
we can prove that $Γ_1 + (Γ_2 + Γ_3) = (Γ_1 + Γ_2) + Γ_3$.

\begin{code}
+-assoc-r  : ∀{Γ Δ Θ Δ′ Θ′} → Γ ≃ Δ + Θ → Θ ≃ Δ′ + Θ′ →
             ∃[ Γ′ ] Γ′ ≃ Δ + Δ′ × Γ ≃ Γ′ + Θ′
+-assoc-l  : ∀{Γ Δ Θ Δ′ Θ′} → Γ ≃ Δ + Θ → Δ ≃ Δ′ + Θ′ →
             ∃[ Γ′ ] Γ′ ≃ Θ′ + Θ × Γ ≃ Δ′ + Γ′
\end{code}
\begin{code}[hide]
+-assoc-r split-e split-e = [] , split-e , split-e
+-assoc-r (split-l p) q with +-assoc-r p q
... | _ , p′ , q′ = _ , split-l p′ , split-l q′
+-assoc-r (split-r p) (split-l q) with +-assoc-r p q
... | _ , p′ , q′ = _ , split-r p′ , split-l q′
+-assoc-r (split-r p) (split-r q) with +-assoc-r p q
... | _ , p′ , q′ = _ , p′ , split-r q′

+-assoc-l p q with +-assoc-r (+-comm p) (+-comm q)
... | Δ , r , p′ = Δ , +-comm r , +-comm p′

+-empty-l : ∀{Γ Δ} → Γ ≃ [] + Δ → Γ ≡ Δ
+-empty-l split-e = refl
+-empty-l (split-r p) = cong (_ ∷_) (+-empty-l p)

+-sing-l : ∀{A B Γ} → [ A ] ≃ B , Γ → A ≡ B × Γ ≡ []
+-sing-l (split-l split-e) = refl , refl
\end{code}

\subsection{Context Permutations}
\label{sec:permutation-agda}

\begin{code}
data _#_ : Context → Context → Set where
  #refl  : ∀{Γ} → Γ # Γ
  #here  : ∀{A B Γ} → (A ∷ B ∷ Γ) # (B ∷ A ∷ Γ)
  #next  : ∀{A Γ Δ} → Γ # Δ → (A ∷ Γ) # (A ∷ Δ)
  #tran  : ∀{Γ Δ Θ} → Γ # Δ → Δ # Θ → Γ # Θ

#sym : ∀{Γ Δ} → Γ # Δ → Δ # Γ
#sym #refl = #refl
#sym #here = #here
#sym (#next π) = #next (#sym π)
#sym (#tran π π′) = #tran (#sym π′) (#sym π)

#empty-inv : ∀{Γ} → [] # Γ → Γ ≡ []
#empty-inv #refl = refl
#empty-inv (#tran π π′) rewrite #empty-inv π | #empty-inv π′ = refl

#singleton-inv : ∀{A Γ} → [ A ] # Γ → Γ ≡ [ A ]
#singleton-inv {Γ = Γ} #refl = refl
#singleton-inv {Γ = Γ} (#next π) rewrite #empty-inv π = refl
#singleton-inv {Γ = Γ} (#tran π π′) rewrite #singleton-inv π | #singleton-inv π′ = refl

#rot : ∀{A B C Γ} → (A ∷ B ∷ C ∷ Γ) # (C ∷ A ∷ B ∷ Γ)
#rot = #tran (#next #here) #here

#cons : ∀{A Γ Δ} → Γ ≃ A , Δ → (A ∷ Δ) # Γ
#cons (split-l p) with +-empty-l p
... | refl = #refl
#cons (split-r p) = #tran #here (#next (#cons p))

#split : ∀{Γ Γ₁ Γ₂ Δ} → Γ # Δ → Γ ≃ Γ₁ + Γ₂ → ∃[ Δ₁ ] ∃[ Δ₂ ] (Δ ≃ Δ₁ + Δ₂ × Γ₁ # Δ₁ × Γ₂ # Δ₂)
#split #refl p = _ , _ , p , #refl , #refl
#split (#next π) (split-l p) with #split π p
... | Δ₁ , Δ₂ , q , π₁ , π₂ = _ ∷ Δ₁ , Δ₂ , split-l q , #next π₁ , π₂
#split (#next π) (split-r p) with #split π p
... | Δ₁ , Δ₂ , q , π₁ , π₂ = Δ₁ , _ ∷ Δ₂ , split-r q , π₁ , #next π₂
#split #here (split-l (split-l p)) = _ , _ , split-l (split-l p) , #here , #refl
#split #here (split-l (split-r p)) = _ , _ , split-r (split-l p) , #refl , #refl
#split #here (split-r (split-l p)) = _ , _ , split-l (split-r p) , #refl , #refl
#split #here (split-r (split-r p)) = _ , _ , split-r (split-r p) , #refl , #here
#split (#tran π π′) p with #split π p
... | Θ₁ , Θ₂ , p′ , π₁ , π₂ with #split π′ p′
... | Δ₁ , Δ₂ , q , π₁′ , π₂′ = Δ₁ , Δ₂ , q , #tran π₁ π₁′ , #tran π₂ π₂′

#one+ : ∀{A Γ Γ′ Δ} → Γ # Δ → Γ ≃ A , Γ′ → ∃[ Δ′ ] (Δ ≃ A , Δ′ × Γ′ # Δ′)
#one+ π p with #split π p
... | _ , _ , q , π₁ , π₂ rewrite #singleton-inv π₁ = _ , q , π₂

#shift : ∀{Γ A Δ} → (Γ ++ A ∷ Δ) # (A ∷ Γ ++ Δ)
#shift {[]} = #refl
#shift {B ∷ Γ} = #tran (#next #shift) #here

+++# : ∀{Γ Γ₁ Γ₂} → Γ ≃ Γ₁ + Γ₂ → (Γ₁ ++ Γ₂) # Γ
+++# split-e = #refl
+++# (split-l p) = #next (+++# p)
+++# (split-r p) = #tran #shift (#next (+++# p))

#left : ∀{Γ Δ Θ} → Γ # Δ → (Θ ++ Γ) # (Θ ++ Δ)
#left {Θ = []} π = π
#left {Θ = _ ∷ Θ} π = #next (#left π)

data Un : Context → Set where
  un-[]  : Un []
  un-∷   : ∀{A Γ} → Un Γ → Un (¿ A ∷ Γ)

#un : ∀{Γ Δ} → Γ # Δ → Un Γ → Un Δ
#un #refl un = un
#un (#next π) (un-∷ un) = un-∷ (#un π un)
#un #here (un-∷ (un-∷ un)) = un-∷ (un-∷ un)
#un (#tran π π′) un = #un π′ (#un π un)

#un+ : ∀{Γ Γ₁ Γ₂} → Γ ≃ Γ₁ + Γ₂ → Un Γ₁ → Un Γ₂ → Un Γ
#un+ split-e un-[] un-[] = un-[]
#un+ (split-l p) (un-∷ un₁) un₂ = un-∷ (#un+ p un₁ un₂)
#un+ (split-r p) un₁ (un-∷ un₂) = un-∷ (#un+ p un₁ un₂)
\end{code}

\subsection{Process Representation}
\label{sec:process-agda}

CO-DE BRUIJN INDICES

We adopt an \emph{intrinsically typed} representation of processes, so that a
process is also a proof derivation showing that the process is well typed. This
choice increases the effort in the definition of the datatypes for processes and
their operational semantics, but it pays off in the rest of the formalization
for a number of reasons:

\begin{itemize}
\item channel names are encoded in the context splitting proofs occurring within
  processes, hence they do not need to be represented explicitly.
\item processes and typing rules are conflated in the same datatype, thus
  reducing the overall number of datatypes in the formalization;
\item the typing preservation results are embedded in the definition of the
  operational semantics of processes and do not need to be proved separately.
\end{itemize}

We now illustrate the definition of the \AgdaDatatype{Process} datatype and
describe each constructor in detail. The datatype is indexed by a typing
context:

\begin{AgdaAlign}
\begin{code}
data Process : Context → Set where
   link      : ∀{Γ A B} (d : Dual A B) (p : Γ ≃ [ A ] + [ B ]) → Process Γ
\end{code}

A \AgdaInductiveConstructor{link} process is well typed in a typing context with
exactly two types $A$ and $B$ which must be related by duality. We use a proof
$p$ that $\Context \simeq [A] + [B]$ instead of requiring $\Context$ to be
simply $[A,B]$ so that it becomes straightforward to define \SLink using
\AgdaFunction{dual-symm} and \AgdaFunction{+-comm}.

\begin{code}
   fail      : ∀{Γ Δ} (p : Γ ≃ ⊤ , Δ) → Process Γ
\end{code}

A \AgdaInductiveConstructor{fail} process simply requires the presence of $\Top$
in the typing context.

\begin{code}
   close     : Process [ 𝟙 ]
\end{code}

A \AgdaInductiveConstructor{close} process is well typed in the singleton
context $[\One]$.

\begin{code}
   wait      : ∀{Γ Δ} (p : Γ ≃ ⊥ , Δ) → Process Δ → Process Γ
\end{code}

A \AgdaInductiveConstructor{wait} process is well typed in any context
$\ContextC$ that contains $\Bot$ and whose residual $\ContextD$ allows the
continuation to be well typed.

\begin{code}
   select    : ∀{Γ Δ A B} (x : Bool) (p : Γ ≃ A ⊕ B , Δ) →
               Process ((if x then A else B) ∷ Δ) → Process Γ
\end{code}

We represent the tags $\InTag_1$ and $\InTag_2$ with Agda′s boolean values
\AgdaInductiveConstructor{true} and \AgdaInductiveConstructor{false}. In this
way we can use a single constructor \AgdaInductiveConstructor{select} for the
selections $\Select\x{\InTag_i}.P$. According to the semantics of \Calculus, the
channel on which the selection is performed is consumed and the continuation
process uses a fresh continuation channel. As a consequence, the type of the
continuation channel, which is either $A$ or $B$ depending on the value of the
tag $x$, is \emph{prepended} in front of the typing context used to type the
continuation process.

\begin{code}
   case      : ∀{Γ Δ A B} (p : Γ ≃ A & B , Δ) →
               Process (A ∷ Δ) → Process (B ∷ Δ) → Process Γ
\end{code}

A \AgdaInductiveConstructor{case} process has two possible continuations. Note
again that the continuation channel, which is received along with the tag, has
either type $A$ or type $B$ depending on the tag and its type is prepended to
the typing context of the continuation process since it is the newest channel.

\begin{code}
   fork      : ∀{Γ Δ Γ₁ Γ₂ A B} (p : Γ ≃ A ⊗ B , Δ) (q : Δ ≃ Γ₁ + Γ₂) →
               Process (A ∷ Γ₁) → Process (B ∷ Γ₂) → Process Γ
\end{code}

A \AgdaInductiveConstructor{fork} process creates two fresh continuation
channels and spaws two continuation processes. Since context splitting is a
ternary relation, we need two splitting proofs $p$ and $q$ to isolate the type
$A \Ten B$ of channel on which the pair of continuation channels is sent and to
distribute the remaining channel across the two continuation processes.

\begin{code}
   join      : ∀{Γ Δ A B} (p : Γ ≃ A ⅋ B , Δ) →
               Process (B ∷ A ∷ Δ) → Process Γ
\end{code}

The \AgdaInductiveConstructor{join} process has a single continuation which uses
the pair of continuation channels received from a channel of type $A \Par B$.
Note that $B$ is prepended in front of $A$, somewhat implying that the second
continuation is bound \emph{after} the first even though, from a technical
standpoint, both continuations are bound simultaneously. We could have prepended
the types $A$ and $B$ also in the opposite order, provided that the reduction
rule \RFork (formalized in \Cref{sec:semantics-agda}) is suitably adjusted.

\begin{code}
   server    : ∀{Γ Δ A} (p : Γ ≃ ¡ A , Δ) (un : Un Δ) →
               Process (A ∷ Δ) → Process Γ
\end{code}

A \AgdaInductiveConstructor{server} constructor introduces a channel of type
$\OfCourse A$ given a continuation process that consumes a channel of type $A$
and a proof $\mathit{un}$ that every other channel in $\ContextD$ is
unrestricted.\Luca{Introdurre terminologia e dire che unrestricted vuol dire che
il canale può essere ``duplicato'' e scartato.}

\begin{code}
   client    : ∀{Γ Δ A} (p : Γ ≃ ¿ A , Δ) → Process (A ∷ Δ) → Process Γ
   weaken    : ∀{Γ Δ A} (p : Γ ≃ ¿ A , Δ) → Process Δ → Process Γ
   contract  : ∀{Γ Δ A} (p : Γ ≃ ¿ A , Δ) → Process (¿ A ∷ ¿ A ∷ Δ) → Process Γ
\end{code}

The constructors \AgdaInductiveConstructor{client},
\AgdaInductiveConstructor{weaken} and \AgdaInductiveConstructor{contract}
introduce a channel of type $\WhyNot A$ and follow the same pattern of the
previous process forms. Note that, in the case of
\AgdaInductiveConstructor{contract}, the contraction of $\WhyNot A$ applies to
the first two channels in the typing context of the continuation. This is
consistent with the fact that \Calculus uses an explicit prefix
$\Contract\x\y\z$ that models contraction and binds $y$ and $z$ in the
continuation process. In a pen-and-paper presentation, like the one by
Wadler~\citep{Wadler14}, it is arguably more convenient to have weaking and
contraction typing rules without the corresponding process forms.

\begin{code}
   cut       : ∀{Γ Γ₁ Γ₂ A B} (d : Dual A B) (p : Γ ≃ Γ₁ + Γ₂) →
               Process (A ∷ Γ₁) → Process (B ∷ Γ₂) → Process Γ
\end{code}

A \AgdaInductiveConstructor{cut} process incorporates a duality proof for the
peer endpoints of the channel being restricted and a splitting proof that
distributes the free channels among the two parallel processes.

\end{AgdaAlign}

\begin{code}
#process : ∀{Γ Δ} → Γ # Δ → Process Γ → Process Δ
\end{code}
\begin{code}[hide]
#process π (link d p) with #one+ π p
... | Δ′ , q , π′ with #singleton-inv π′
... | refl = link d q
#process π close with #singleton-inv π
... | refl = close
#process π (fail p) with #one+ π p
... | Δ′ , q , π′ = fail q
#process π (wait p P) with #one+ π p
... | Δ′ , q , π′ = wait q (#process π′ P)
#process π (select x p P) with #one+ π p
... | Δ′ , q , π′ = select x q (#process (#next π′) P)
#process π (case p P Q) with #one+ π p
... | Δ′ , q , π′ = case q (#process (#next π′) P) (#process (#next π′) Q)
#process π (fork p q P Q) with #one+ π p
... | Δ′ , p′ , π′ with #split π′ q
... | Δ₁ , Δ₂ , q′ , π₁ , π₂ = fork p′ q′ (#process (#next π₁) P) (#process (#next π₂) Q)
#process π (join p P) with #one+ π p
... | Δ′ , q , π′ = join q (#process (#next (#next π′)) P)
#process π (cut d p P Q) with #split π p
... | Δ₁ , Δ₂ , q , π₁ , π₂ = cut d q (#process (#next π₁) P) (#process (#next π₂) Q)
#process π (server p un P) with #one+ π p
... | Δ′ , q , π′ = server q (#un π′ un) (#process (#next π′) P)
#process π (client p P) with #one+ π p
... | Δ′ , q , π′ = client q (#process (#next π′) P)
#process π (weaken p P) with #one+ π p
... | Δ′ , q , π′ = weaken q (#process π′ P)
#process π (contract p P) with #one+ π p
... | Δ′ , q , π′ = contract q (#process (#next (#next π′)) P)
\end{code}

\subsection{Operational Semantics}
\label{sec:semantics-agda}

We formalize structural precongruence as a binary relation between processes
that are well typed in the \emph{same} typing context. This entails that
structural precongruence preserves typing by definition.

\begin{AgdaAlign}
\begin{code}
data _⊒_ : ∀{Γ} → Process Γ → Process Γ → Set where
\end{code}

The datatype for $\pcong$ has one constructor for each of the structural
precongruence rules in \Cref{tab:semantics}. Since many aspects recur
repeatedly, we will focus on just a few representative rules starting from
\SComm.

\begin{code}
  s-comm :
    ∀{Γ Γ₁ Γ₂ A B P Q} (d : Dual A B) (p : Γ ≃ Γ₁ + Γ₂) →
    cut d p P Q ⊒ cut (dual-symm d) (+-comm p) Q P
\end{code}

The constructor \AgdaInductiveConstructor{s-comm} models the commutativity
property of parallel composition. We use \AgdaFunction{dual-symm} and
\AgdaFunction{+-comm} to compute the proofs of the relation
$\AgdaDatatype{Dual}~B~A$ and of the splitting $\ContextC \simeq \Context_2 +
\Context_1$ from $d$ and $p$ respectively.

\begin{code}[hide]
  s-link :
    ∀{Γ A B}
    (d : Dual A B) (p : Γ ≃ [ A ] + [ B ]) →
    link d p ⊒ link (dual-symm d) (+-comm p)

  s-fail :
    ∀{Γ Γ₁ Γ₂ Δ A B P} (d : Dual A B)
    (p : Γ ≃ Γ₁ + Γ₂) (q : Γ₁ ≃ ⊤ , Δ) →
    let _ , _ , q′ = +-assoc-l p q in
    cut d p (fail (split-r q)) P ⊒ fail q′
\end{code}

The constructor \AgdaInductiveConstructor{s-wait} models the \SWait rule:

\begin{code}
  s-wait :
    ∀{Γ Γ₁ Γ₂ Δ A B} {P : Process (A ∷ Δ)} {Q : Process (B ∷ Γ₂)}
    (d : Dual A B) (p : Γ ≃ Γ₁ + Γ₂) (q : Γ₁ ≃ ⊥ , Δ) →
    let _ , p′ , q′ = +-assoc-l p q in
    cut d p (wait (split-r q) P) Q ⊒ wait q′ (cut d p′ P Q)
\end{code}

There are two non-trivial aspects worth commenting. The first one concerns the
proof $(\AgdaInductiveConstructor{split-r}~q)$ used in the
\AgdaInductiveConstructor{wait} process before structural precongruence is
applied. To understand the meaning of this proof, we must recall three key
elements:
\begin{enumerate}
  \item
    $(\AgdaInductiveConstructor{wait}~(\AgdaInductiveConstructor{split-r}~q)~P)$
    is a direct sub-process of the \AgdaInductiveConstructor{cut}, and therefore
    it is meant to be well typed in the context
    $A~\AgdaInductiveConstructor{∷}~\Context_1$.
  \item Being a \AgdaInductiveConstructor{wait} process, such context must
    contain a $\Bot$ type as per the typing rule \WaitRule. That is
    $A~\AgdaInductiveConstructor{∷}~\Context_1 \simeq [\Bot] +
    A~\AgdaInductiveConstructor{∷}~\Delta$ for some $\Delta$.
  \item The \SWait rule is applicable only provided that the channel restricted
    by the cut (say $x$, of type $A$) is different from the channel (say $y$, of
    type $\Bot$) consumed by the \AgdaInductiveConstructor{wait} process.
    %
    We enforce the side condition $x \ne y$ of \SWait imposing that the type $A$
    in front of $A~\AgdaInductiveConstructor{∷}~\Context_1$ necessarily comes
    from the right component of the splitting $[\Bot] +
    A~\AgdaInductiveConstructor{∷}~\Delta$ through the use of
    \AgdaInductiveConstructor{split-r}.
\end{enumerate}

The other aspect that is worth commenting concerns the rearrangement of the
splittings in the process after the application of structural precongruence.
Overall, $p$ and $q$ prove the splittings $([\Bot] + \ContextD) + \ContextC_2$,
but the precongruence rule requires this splitting to be rearranged as $[\Bot] +
(\ContextD + \ContextC_2)$. That is, we need to apply the left-to-right
associativity property of context splitting which we called
\AgdaFunction{+-assoc-l} in \Cref{sec:context-agda}. The nested
\AgdaKeyword{let}-\AgdaKeyword{in} allows us to pattern match the result of the
application $\AgdaFunction{+-assoc-l}~p~q$ and to extract the new proofs $p'$
and $q'$ for the rearranged splittings.

The constructors \AgdaInductiveConstructor{s-select-l} and
\AgdaInductiveConstructor{s-select-r} model \SSelect when the selected tag is
respectively \AgdaInductiveConstructor{true} and
\AgdaInductiveConstructor{false}.

\begin{code}
  s-select-l :
    ∀{Γ Γ₁ Γ₂ Δ A B C D} {P : Process (C ∷ A ∷ Δ)} {Q : Process (B ∷ Γ₂)}
    (d : Dual A B) (p : Γ ≃ Γ₁ + Γ₂) (q : Γ₁ ≃ C ⊕ D , Δ) →
    let _ , p′ , q′ = +-assoc-l p q in
    cut d p (select true (split-r q) P) Q ⊒
    select true q′ (cut d (split-l p′) (#process #here P) Q)
\end{code}

Here the process
$(\AgdaInductiveConstructor{select}~\AgdaInductiveConstructor{true}~(\AgdaInductiveConstructor{split-r}~q)~P)$,
which is found under a cut for $x : A$, is using some channel $y : C \Plus D$ to
send the tag $\AgdaInductiveConstructor{true}$.
%
Unlike \AgdaInductiveConstructor{wait}, which consumes the channel it operates
on, the continuation process $P$ will be using a fresh continuation channel $z :
C$. Therefore, $P$ is required to be well typed in the context
$C~\AgdaInductiveConstructor{∷}~A~\AgdaInductiveConstructor{∷}~\ContextD$, where
the type of the continuation sits on top of the type of the restricted channel.
%
After structural precongruence is applied, however, the type $C$ of the
continuation channel $z$ ends up underneath that of the restricted channel $x$,
because now the two channels are introduced in the opposite order. Therefore, we
are in a situation where we need to turn $P$ into a process that is well typed
in the context
$A~\AgdaInductiveConstructor{∷}~C~\AgdaInductiveConstructor{∷}~\ContextD$. This
is achieved applying the function \AgdaFunction{\#process} to the
\AgdaInductiveConstructor{\#here} permutation, which swaps the two topmost types
in a typing context, and finally to the process $P$.

\begin{code}[hide]
  s-select-r :
    ∀{Γ Γ₁ Γ₂ Δ A B C D}
    {P : Process (D ∷ A ∷ Δ)}
    {Q : Process (B ∷ Γ₂)}
    (d : Dual A B) (p : Γ ≃ Γ₁ + Γ₂) (q : Γ₁ ≃ C ⊕ D , Δ) →
    let _ , p′ , q′ = +-assoc-l p q in
    cut d p (select false (split-r q) P) Q ⊒
    select false q′ (cut d (split-l p′) (#process #here P) Q)

  s-case :
    ∀{Γ A B A₁ A₂ Γ₁ Γ₂ Δ}
    {P : Process (A₁ ∷ A ∷ Δ)}
    {Q : Process (A₂ ∷ A ∷ Δ)}
    {R : Process (B ∷ Γ₂)}
    (d : Dual A B)
    (p : Γ ≃ Γ₁ + Γ₂) (q : Γ₁ ≃ A₁ & A₂ , Δ) →
    let _ , p′ , q′ = +-assoc-l p q in
    cut d p (case (split-r q) P Q) R ⊒
    case q′ (cut d (split-l p′) (#process #here P) R)
            (cut d (split-l p′) (#process #here Q) R)
\end{code}

\emph{En passant}, we also show the modeling of the \SForkL rule, which is
interesting because of its complex side conditions:

\begin{code}
  s-fork-l :
    ∀{Γ Γ₁ Γ₂ Δ Δ₁ Δ₂ A B C D}
    {P : Process (C ∷ A ∷ Δ₁)} {Q : Process (D ∷ Δ₂)} {R : Process (B ∷ Γ₂)}
    (d : Dual A B) (p : Γ ≃ Γ₁ + Γ₂) (q : Γ₁ ≃ C ⊗ D , Δ) (r : Δ ≃ Δ₁ + Δ₂) →
    let _ , p′ , q′ = +-assoc-l p q in
    let _ , p′′ , r′ = +-assoc-l p′ r in
    let _ , q′′ , r′′ = +-assoc-r r′ (+-comm p′′) in
    cut d p (fork (split-r q) (split-l r) P Q) R ⊒
    fork q′ r′′ (cut d (split-l q′′) (#process #here P) R) Q
\end{code}

Recall from \Cref{tab:semantics} that we want to apply this rule on a process of
the form $\Cut\x{\Fork\y\u\v{P}{Q}}{R}$ when $x \in \fn{P}$. We capture this
condition by means of the splitting $(\AgdaInductiveConstructor{split-l}~r)$,
meaning that the type $A$ of $x$ ends up in the typing context for $P$ and not
in the one for $Q$.
%
The symmetric rule \SForkR is modeled by another constructor
\AgdaInductiveConstructor{s-fork-r}, which is not shown here but is similar to
\AgdaInductiveConstructor{s-fork-l} except that
$(\AgdaInductiveConstructor{split-l}~r)$ is replaced by
$(\AgdaInductiveConstructor{split-r}~r)$.

\begin{code}[hide]
  s-fork-r :
    ∀{Γ Γ₁ Γ₂ Δ Δ₁ Δ₂ A B C D}
    {P : Process (C ∷ Δ₁)}
    {Q : Process (D ∷ A ∷ Δ₂)}
    {R : Process (B ∷ Γ₂)}
    (d : Dual A B) (p : Γ ≃ Γ₁ + Γ₂) (q : Γ₁ ≃ C ⊗ D , Δ)
    (r : Δ ≃ Δ₁ + Δ₂) →
    let _ , p′ , q′ = +-assoc-l p q in
    let _ , p′′ , r′ = +-assoc-l p′ r in
    cut d p (fork (split-r q) (split-r r) P Q) R ⊒
    fork q′ r′ P (cut d (split-l p′′) (#process #here Q) R)

  s-join :
    ∀{Γ Γ₁ Γ₂ Δ A B C D}
    {P : Process (D ∷ C ∷ A ∷ Δ)}
    {Q : Process (B ∷ Γ₂)}
    (d : Dual A B) (p : Γ ≃ Γ₁ + Γ₂) (q : Γ₁ ≃ C ⅋ D , Δ) →
    let _ , p′ , q′ = +-assoc-l p q in
    cut d p (join (split-r q) P) Q ⊒
    join q′ (cut d (split-l (split-l p′)) (#process #rot P) Q)

  s-server :
    ∀{Γ A B C Γ₁ Γ₂ Δ₁}
    {P : Process (C ∷ ¿ A ∷ Δ₁)}
    {Q : Process (B ∷ Γ₂)}
    (d : Dual A B) (p : Γ ≃ Γ₁ + Γ₂) (q : Γ₁ ≃ ¡ C , Δ₁) (r : Γ₂ ≃ [] + Γ₂)
    (un₁ : Un Δ₁) (un₂ : Un Γ₂) →
    let _ , p′ , q′ = +-assoc-l p q in
    cut (d-?-! d) p (server (split-r q) (un-∷ un₁) P) (server (split-l r) un₂ Q) ⊒
    server q′ (#un+ p′ un₁ un₂) (cut (d-?-! d) (split-l p′) (#process #here P) (server (split-l r) un₂ Q))

  s-client :
    ∀{Γ A B C Γ₁ Γ₂ Δ}
    {P : Process (C ∷ A ∷ Δ)}
    {Q : Process (B ∷ Γ₂)}
    (d : Dual A B) (p : Γ ≃ Γ₁ + Γ₂) (q : Γ₁ ≃ ¿ C , Δ) →
    let _ , p′ , q′ = +-assoc-l p q in
    cut d p (client (split-r q) P) Q ⊒
    client q′ (cut d (split-l p′) (#process #here P) Q)

  s-weaken :
    ∀{Γ A B C Γ₁ Γ₂ Δ}
    {P : Process (A ∷ Δ)}
    {Q : Process (B ∷ Γ₂)}
    (d : Dual A B) (p : Γ ≃ Γ₁ + Γ₂) (q : Γ₁ ≃ ¿ C , Δ) →
    let _ , p′ , q′ = +-assoc-l p q in
    cut d p (weaken (split-r q) P) Q ⊒
    weaken q′ (cut d p′ P Q)

  s-contract :
    ∀{Γ A B C Γ₁ Γ₂ Δ}
    {P : Process (¿ C ∷ ¿ C ∷ A ∷ Δ)}
    {Q : Process (B ∷ Γ₂)}
    (d : Dual A B) (p : Γ ≃ Γ₁ + Γ₂) (q : Γ₁ ≃ ¿ C , Δ) →
    let _ , p′ , q′ = +-assoc-l p q in
    cut d p (contract (split-r q) P) Q ⊒
    contract q′ (cut d (split-l (split-l p′)) (#process #rot P) Q)
\end{code}

Finally, in \Cref{sec:semantics} we have colloquially defined $\pcong$ as a
``\emph{pre-congruence}'', implicitly implying that it is reflexive, transitive,
and that it is is preserved by some forms of calculus. In the formalization we
have to be precise and we introduce specific rules:

\begin{code}
  s-refl  : ∀{Γ} {P : Process Γ} → P ⊒ P
  s-tran  : ∀{Γ} {P Q R : Process Γ} → P ⊒ Q → Q ⊒ R → P ⊒ R
  s-cong  : ∀{Γ Γ₁ Γ₂ A A′} {P Q : Process (A ∷ Γ₁)} {P′ Q′ : Process (A′ ∷ Γ₂)}
            (d : Dual A A′) (p : Γ ≃ Γ₁ + Γ₂) → P ⊒ Q → P′ ⊒ Q′ →
            cut d p P P′ ⊒ cut d p Q Q′
\end{code}

Note that we define a single congruence rule \AgdaInductiveConstructor{s-cong}
that allows us to apply $\pcong$ within cuts, but not underneath prefixes. This
limited form of pre-congruence turns out to be sufficient for the development
that follows.
\end{AgdaAlign}

